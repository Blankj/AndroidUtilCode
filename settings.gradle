includeModule\
        ':buildSrc',
        ':bus-gradle-plugin',
        ':launcher',
        ':lib',
        ':subutil',
        ':utilcode'

// 如果 name 以 '-' 开头，则剔除在外
def includeModule(String... names) {
    names.each {
        if (it.startsWith('-')) return
        def fileName
        if (it.startsWith(":")) {
            fileName = it.substring(1, it.length())
        } else {
            fileName = it;
        }
        File f = file(fileName)
        if (!f.exists() || !f.isDirectory()) return
        if (isModule(f)) {
            include it
        } else {
            f.eachDir { dir ->
                if (!isModule(dir)) return
                include "${fileName}-${dir.name}"
                project(":${fileName}-${dir.name}").projectDir = dir
            }
        }
    }
}

static boolean isModule(File dir) {
    File[] files = dir.listFiles(new FilenameFilter() {
        @Override
        boolean accept(File file, String name) {
            return name == 'build.gradle' || name == 'src'
        }
    })
    return files.size() == 2
}



